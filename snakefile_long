configfile: 'config.yml'

rule all:
    input:
        expand('results/annotation/abricate_{sample}.tab',sample=config['samples']),
        expand('results/annotation/prokka_{sample}.tsv',sample=config['samples']),
        expand('results/annotation/sprot_{sample}.tsv',sample=config['samples'])

rule flye: 
    input:
        'data/{sample}.fastq' #can be more than 1 input file
    output:
        folder=directory('results/flye/{sample}'),
        fasta='results/flye/{sample}/assembly.fasta'
    conda:
        'env/flye.yml'
    params:
        read_type=config['read_type']
    threads: 
        config['threads']
    shell:
        'flye {params.read_type} {input} --threads {threads} -o {output.folder}'

rule abricate_plasmid:
    input:
        'results/flye/{sample}/assembly.fasta'
    output:
        'results/abricate_plasmid/{sample}.tab'
    conda:
        'env/abricate.yml'
    threads: 4
    shell:
        'abricate -db plasmidfinder {input} > {output}'

rule contig_plasmid:
    input:
        'results/abricate_plasmid/{sample}.tab'
    output:
        'results/contigs_plasmid/{sample}.fasta'
    threads: 2
    script:
        'scripts/contig_plasmid.py'

rule plasmid_amr:
    input:
        'results/contigs_plasmid/{sample}.fasta'
    output:
        'results/annotation/abricate_{sample}.tab'
    conda:
        'env/abricate.yml'
    threads: 4
    shell:
        'abricate {input} > {output}'

rule prokka:
    input:
        'results/contigs_plasmid/{sample}.fasta'
    output:
        folder=directory('results/prokka_plasmid/{sample}'),
        tsv='results/prokka_plasmid/{sample}/{sample}.tsv',
        ffn='results/prokka_plasmid/{sample}/{sample}.ffn',
        dest='results/annotation/prokka_{sample}.tsv'
    conda:
        'env/prokka.yml'
    threads: 8
    shell:
        'prokka --outdir {output.folder} --prefix {wildcards.sample} {input} --force && '
        'cp {output.tsv} {output.dest}'
    
rule wget_uniprot:
    input:

    output:
        temp('bin/swissprot/uniprot_sprot.fasta')
    params:
        'https://ftp.uniprot.org/pub/databases/uniprot/current_release/knowledgebase/complete/uniprot_sprot.fasta.gz'
    threads: 2 
    shell:
        'wget {params} -P bin/swissprot && '
        'gunzip {output}.gz'

rule makeblastdb:
    input:
        uniprot_fasta='bin/swissprot/uniprot_sprot.fasta'
    output:
        phr='bin/swissprot/swissprot.phr',
        pin='bin/swissprot/swissprot.pin',
        psq='bin/swissprot/swissprot.psq'
    conda:
        'env/blast.yml'
    params:
        db='bin/swissprot/swissprot'
    threads: 4        
    shell:
        'makeblastdb -dbtype prot -in {input.uniprot_fasta} -out {params.db}'

rule swissprot_blastx:
    input:
        ffn='results/prokka_plasmid/{sample}/{sample}.ffn',
        phr='bin/swissprot/swissprot.phr',
        pin='bin/swissprot/swissprot.pin',
        psq='bin/swissprot/swissprot.psq'
    output:
        'results/annotation/temp/swissprot_blastx_{sample}.tsv'
    conda:
        'env/blast.yml'
    params:
        db='bin/swissprot/swissprot',
        outfmt='6',
        evalue='0.1',
        max_hsps='1'
        max_seqs='10'
    threads: 4
    shell:
        'blastx -query {input.ffn} -db {params.db}'
        ' -out {output} -outfmt {params.outfmt} -evalue {params.evalue} -max_hsps {params.max_hsps} -max_target_seqs {params.max_seqs} -num_threads {threads}'

rule uniprot_query:
    input:
        'results/annotation/temp/swissprot_blastx_{sample}.tsv'
    output:
        'results/annotation/temp/swissprot_query_{sample}.tsv'
    threads: 2
    script:
        'scripts/uniprot_query.py'

rule keep_best:
    input:
        'results/annotation/temp/swissprot_query_{sample}.tsv'
    output:
        'results/annotation/sprot_{sample}.tsv'
    threads: 2
    script:
        'scripts/sprot_keep_best.py'